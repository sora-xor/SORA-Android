plugins {
    id 'com.android.library'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.google.devtools.ksp'
}
apply from: '../tests.gradle'

kotlin {
    jvmToolchain(11)
}

android {
    compileSdkVersion compileVersion

    defaultConfig {
        minSdkVersion minVersion
        targetSdkVersion targetVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled soraMinifyRelease
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "default"

    productFlavors {
        develop {
            dimension 'default'
        }

        soralution {
            dimension 'default'
        }

        tsting {
            dimension 'default'
        }

        production {
            dimension 'default'
        }
    }
    namespace 'com.example.core_db'

    kotlin {
        jvmToolchain(11)
    }
}

class RoomSchemaArgProvider implements CommandLineArgumentProvider {

    @InputDirectory
    @PathSensitive(PathSensitivity.RELATIVE)
    File schemaDir

    RoomSchemaArgProvider(File schemaDir) {
        this.schemaDir = schemaDir
    }

    @Override
    Iterable<String> asArguments() {
        // Note: If you're using KSP, change the line below to return
        // ["room.schemaLocation=${schemaDir.path}".toString()].
        return ["room.schemaLocation=${schemaDir.path}".toString()]
    }
}

ksp {
    arg(new RoomSchemaArgProvider(new File(projectDir, "schemas")))
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(":common")

    implementation daggerDep
    kapt daggerKaptDep

    implementation coroutineDep
    implementation coroutineAndroidDep

    implementation roomDep
    implementation roomKtxDep
    ksp roomKaptDep

    androidTestImplementation project(":test_shared")
    androidTestImplementation project(":test_data")
    androidTestImplementation androidxTestExtJunitDep
    androidTestImplementation androidxTestEspressoCoreDep
    androidTestImplementation archCoreTestDep
}